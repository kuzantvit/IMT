query CheckAsset($host) from CloudDC_Assets {
	host.ipaddress == $host
    or host.fqdn == $host
	or host.hostname == $host
}

query CheckSpecificValueWhitelist($host_name, $user_id, $specific_value) from Sibintek_MITRE_ATTCK_whitelist {
    (host == $host_name or host == "*")
    and (user_id == $user_id or user_id == "*")
    and (specific_value == $specific_value or specific_value == "*")
    and (rule == "SIB_Bruteforce_attempt_mult_acc_from_src" or rule == "*")
}

event Bruteforce_attempt_multiple_acc_frm_src:
    key:
        event_src.host
    filter {
		in_list([2, 3, 7, 10], logon_type)
        and exec_query("CheckAsset", [recv_ipv4])
		and subject == "account"
		and action == "login"
		and status == "failure"
        and correlation_name == null
		and event_src.title == "windows"
		and not match(lower(subject.name), "dwm-*")  # dwm-[dd]
        and not match(lower(subject.name),"umfd-??")  # umfd-[dd]
		and not exec_query("CheckSpecificValueWhitelist", [
            		lower(event_src.host),
            		lower(subject.id),
                    lower(subject.name)
		])
        and not match(lower(event_src.hostname), "inf-dc-*")
        and not match(lower(event_src.hostname), "cdc-rdgw*")
        and not match(lower(event_src.hostname), "inf-exch*")
        and not match(lower(event_src.hostname), "inf-adfs*")
		and msgid == "4625"
    }

rule SIB_Bruteforce_attempt_mult_acc_from_src: (Bruteforce_attempt_multiple_acc_frm_src[10,] with different subject.name) timer 2m
    on Bruteforce_attempt_multiple_acc_frm_src
    {
        $event_src.host = event_src.host
        $event_src.fqdn = event_src.fqdn
        $event_src.hostname = event_src.hostname
        $event_src.asset = event_src.asset
        $event_src.title = event_src.title
        $event_src.vendor = event_src.vendor
        $subject.domain = subject.domain
    }

emit {

	$correlation_type = "incident"
    $category.generic = "Attacks & Recon"
    $category.high = "Attack"
    $category.low = "Bruteforce"
    $importance = "high"
    $id = "SIB_Bruteforce_attempt_mult_acc_from_src"
}